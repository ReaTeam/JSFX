desc: Note On under X velocity to Note Off and send All Notes Off
author: Michael Schnell (mschnell@bschnell.de)
version: 1.1
changelog: allow for setting vel = 0 -> pass through
about:
  ## Description
      Based on "X-Raym_Note On under X velocity to Note Off"
      Kills all notes below a selectable velocity threshold and sends an "All Notes Off" message when a Note-On with velocity below this threshold is found, or when a selectable CC control drops below another selectable threshold
      "Input Channel select a dedicated channel to work on or "All"
      "Velocity Threshold" sets the Note-on Velocity below that all notes are killed
      "Controller" selects the CC controller # to work on
      "Controller Threshold" sets the CC value below that an "All sound Off" message is sent.

slider1:0<0,16,1{Any,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16}>Input Channel
slider2:0<0,127,1>Velocity Threshold
slider3:2<0,129,1{0 Bank Sel M,1 Mod Wheel M,2 Breath M,3,4 Foot P M,5 Porta M,6 Data Entry M,7 Vol M,8 Balance M,9,10 Pan M,11 Expression M,12 Ctrl 1 M,13 Ctrl 2 M,14,15,16 GP Slider 1,17 GP Slider 2,18 GP Slider 3,19 GP Slider 4,20,21,22,23,24,25,26,27,28,29,30,31,32 Bank Sel L,33 Mod Wheel L,34 Breath L,35,36 Foot P L,37 Porta L,38 Data Entry L,39 Vol L,40 Balance L,41,42 Pan L,43 Expression L,44 Ctrl 1 L,45 Ctrl 2 L,46,47,48,49,50,51,52,53,54,55,56,57,58,59,60,61,62,63,64 Hold P sw,65 Porta sw,66 Sustenuto sw,67 Soft P sw,68 Legato P sw,69 Hold 2 P sw,70 S.Variation,71 S.Timbre,72 S.Release,73 S.Attack,74 S.Brightness,75 S.Ctrl 6,76 S.Ctrl 7,77 S.Ctrl 8,78 S.Ctrl 9,79 S.Ctrl 10,80 GP B.1 sw,81 GP B.2 sw,82 GP B.3 sw,83 GP B.4 sw,84,85,86,87,88,89,90,91 Effects Lv,92 Trem Lv,93 Chorus Lv,94 Celeste Lv,95 Phaser Lv,96 Data B. Inc,97 Data B. Dec,98 NRP L,99 NRP M,100 RP L,101 RP M,102,103,104,105,106,107,108,109,110,111,112,113,114,115,116,117,118,119,120,121,122,123,124,125,126,127,Pitch Wheel, Channel Pressure}>Controller
slider4:0<0,127,1>Controller Threshold

////////////////////////////////////////////////////////////////////////////////
@init
statNoteOn  = $x90;
statNoteOff = $x80;
statCC      = 0xB0;
stattAllOff = 123;


////////////////////////////////////////////////////////////////////////////////
@slider
inChannel = slider1 - 1;
oldvel = -1;

////////////////////////////////////////////////////////////////////////////////
@block
while (
  midirecv(offset,msg1,note,vel) ? (
    // Extract message type and channel
    status = msg1 & $xF0;
    channel = msg1 & $x0F;
    // Is it on our channel?
    channel == inChannel || inChannel == -1 ? (      
      // Is it a note event?
      status == statNoteOn ? (
          // If note is good
        vel < slider2 ? (              
          vel = 0;
          msg1 = statNoteOff;         
          msg1_ = channel | statCC;
          midisend(offset,msg1_,stattAllOff,0);    
        );
       ) : status == statCC ? (
        note == slider3 ? (
          vel_ = vel < slider4;
          oldvel_ != vel_ ? (
            oldvel_ = vel_;
            vel_ ? (
              msg1_ = channel | statCC;
              midisend(offset,msg1_,stattAllOff,0);    
            );
          );
        );
      );
    );     
    midisend(offset,msg1,note,vel);    
    1; // Force loop to continue until all messages have been processed
  );
);
