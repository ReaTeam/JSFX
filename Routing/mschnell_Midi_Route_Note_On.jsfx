desc: Route Note On
author: Michael Schnell (mschnell@bschnell.de)
version: 1.0
changelog: initial release
about:
  ## Description
  
    The plugin works with four Midi Buses: 
  
    "Command" accepts a definable Midi CC message to switch the routing to "low", "high" or "both", depending on the "Max Low" and "Min High" settings.
  
    The Midi message in the "In" Bus are routed to the "Low" and "High" out Buses appropriately.  
    All messages other than "Note On" and "Hold Pedal=press" are sent to both out buses. "Note On" and "Hold Pedal=press" are sent according to the current routing definition state.

slider1:  0<0,15,1{1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16}>Command Midi Bus 
slider2:  1<0,15,1{1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16}>In Midi Bus 
slider3:  2<0,15,1{1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16}>Out Low Midi Bus 
slider4:  3<0,15,1{1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16}>Out High Midi Bus 
slider5:  0<0,15,1{1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16}>Command Midi Channel 
slider6:  7<0,127,1{0 Bank Sel M,1 Mod Wheel M,2 Breath M,3,4 Foot P M,5 Porta M,6 Data Entry M,7 Vol M,8 Balance M,9,10 Pan M,11 Expression M,12 Ctrl 1 M,13 Ctrl 2 M,14,15,16 GP Slider 1,17 GP Slider 2,18 GP Slider 3,19 GP Slider 4,20,21,22,23,24,25,26,27,28,29,30,31,32 Bank Sel L,33 Mod Wheel L,34 Breath L,35,36 Foot P L,37 Porta L,38 Data Entry L,39 Vol L,40 Balance L,41,42 Pan L,43 Expression L,44 Ctrl 1 L,45 Ctrl 2 L,46,47,48,49,50,51,52,53,54,55,56,57,58,59,60,61,62,63,64 Hold P sw,65 Porta sw,66 Sustenuto sw,67 Soft P sw,68 Legato P sw,69 Hold 2 P sw,70 S.Variation,71 S.Timbre,72 S.Release,73 S.Attack,74 S.Brightness,75 S.Ctrl 6,76 S.Ctrl 7,77 S.Ctrl 8,78 S.Ctrl 9,79 S.Ctrl 10,80 GP B.1 sw,81 GP B.2 sw,82 GP B.3 sw,83 GP B.4 sw,84,85,86,87,88,89,90,91 Effects Lv,92 Trem Lv,93 Chorus Lv,94 Celeste Lv,95 Phaser Lv,96 Data B. Inc,97 Data B. Dec,98 NRP L,99 NRP M,100 RP L,101 RP M,102,103,104,105,106,107,108,109,110,111,112,113,114,115,116,117,118,119,120,121,122,123,124,125,126,127}>Command CC#
slider7: 63<0,127,1>Max Low
slider8: 64<0,127,1>Min High

@init
  ext_midi_bus = 1;
  NOTE_ON      =  0x90;
  CC_MSG       =  0xB0;   
  HOLDP        =  64;

  
@slider
  comBus     = slider1;
  inBus      = slider2;
  lowBus     = slider3;
  highBus    = slider4;
  comChannel = slider5;
  comCC      = slider6;  
  maxLow     = slider7;
  minHigh    = slider8;
  low        = 0;
  high       = 0;
  
  com        = CC_MSG + comChannel;


@block
while(midirecv(mpos, msg1, msg2, msg3)) (
  midi_bus == comBus ? (
    midisend(mpos, msg1, msg2, msg3);                                 // pass threough
    (msg1 == com) && (msg2 == comCC) ?  (
      low  = msg3 <= maxLow  ? 1 : 0;
      high = msg3 >= minHigh ? 1 : 0;
    );  
   ) : midi_bus == inBus ? (
    route = 0;
    status = msg1 & 0xF0;
    (status == CC_MSG) && (msg2 == HoldP) && (msg3 > 64) ? route = 1; // press hold pedal
    (status == NOTE_ON) && (msg3)                        ? route = 1; // press key                    
    !route || low ? (
       midi_bus = lowBus;
       midisend(mpos, msg1, msg2, msg3);
    );
    !route || high ? (
       midi_bus = highBus;
       midisend(mpos, msg1, msg2, msg3);
    );
   ) : (  
    midisend(mpos, msg1, msg2, msg3);
  );  
);  
