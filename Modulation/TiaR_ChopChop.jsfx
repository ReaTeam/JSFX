desc: Stereo Chop chop
version: 1.0
author: T.Rochebois
changelog: Initial Release
link: https://www.youtube.com/watch?v=LHbSUXsJ82M

/*
 Copyright 2015, Thierry Rochebois
 All rights reserved.

  Redistribution and use in source and binary forms, with or without 
  modification, are permitted provided that the following conditions are
  met:
  Redistributions of source code must retain the above copyright notice,
  this list of conditions and the following disclaimer. 

  Redistributions in binary form must reproduce the above copyright
  notice, this list of conditions and the following disclaimer in the
  documentation and/or other materials provided with the distribution. 

  The name of Thierry Rochebois may not be used to endorse or promote
  products derived from this software without specific prior written
  permission. 

  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS 
  "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
  LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
  A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT 
  OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
  SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT 
  LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
  DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
  THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT 
  (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
  OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/


slider1:1<0,5,1{1,2,4,8,16,32}>Pattern length

slider11:0<-1,1>Left - time offset
slider12:4<0,8>Left - Density
slider13:0.7<0,1>Left - Gate length
slider14:1<0,8,1{Reg,Swg,OffBt 1,Clust,Raf,OffBt 2,Forward,Backward,ZigZag}>Left - Type
slider15:-3<-3,0>Left - Attack
slider16:-3<-3,0>Left - Release
slider17:0<0,1,1{direct,invert}>Left - Invert

slider21:0<-1,1>Right - time offset
slider22:4<0,8>Right - Density
slider23:0.7<0,1>Right - Gate length
slider24:1<0,8,1{Reg,Swg,OffBt 1,Clust,Raf,OffBt 2,Forward,Backward,ZigZag}>Right - Type
slider25:-3<-3,0>Right - Attack
slider26:-3<-3,0>Right - Release
slider27:0<0,1,1{direct,invert}>Right - Invert
// ___________________________________________________________________
@init
                   function pat(a b c d e f g h)(
  p[0]=a;p[1]=b;p[2]=c;p[3]=d;p[4]=e;p[5]=f;p[6]=g;p[7]=h; p += 8;);
                       _srate = 1 / srate;
   pat(0,7,3,5,1,6,2,4); pat(0,7,4,1,5,6,2,3); pat(7,2,0,4,6,3,1,5);
   pat(0,1,7,4,6,2,3,5); pat(0,1,2,7,3,5,4,6); pat(2,7,0,4,5,1,3,6);
   pat(0,1,2,3,4,5,6,7); pat(0,7,6,5,4,3,2,1); pat(7,0,6,2,5,2,4,3);
l.coef = p; p += 2;                                        r.coef = p;
// ___________________________________________________________________
@slider
                    function tcoef(x)(_srate*10^-x;);
                       _patLength = 2 ^ -slider1;
l.pattern = 8 * slider14|0;                r.pattern = 8 * slider24|0;
l.coef[0] = tcoef(slider15);              r.coef[0] = tcoef(slider25);
l.coef[1] = tcoef(slider16);              r.coef[1] = tcoef(slider26);
// ___________________________________________________________________
@block
function frac(x)(x-floor(x););
play_state > 0 ? 
  bp = beat_position
: bp += tempo * (1/60) * samplesblock * _srate - 151200 * (bp > 151200);
sbp = bp * _patLength;
l.b = 8 * frac(sbp + slider11);        r.b = 8 * frac(sbp + slider21); 
l.gate = (l.pattern[l.b] <= slider12) & (frac(l.b) < slider13);
r.gate = (r.pattern[r.b] <= slider22) & (frac(r.b) < slider23);
slider17 ? l.gate = !l.gate;              slider27 ? r.gate = !r.gate;
// ___________________________________________________________________
@sample
spl0 *= (l.env += l.coef[l.gate < l.env] * (l.gate - l.env));
spl1 *= (r.env += r.coef[r.gate < r.env] * (r.gate - r.env));