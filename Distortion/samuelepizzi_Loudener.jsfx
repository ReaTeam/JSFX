desc: Loudener
author: Samuele Pizzi
version: 1.0
provides:
  loudener_gfx/bg.png https://raw.githubusercontent.com/samu4199/Loudener/main/loudener_gfx/bg.png
  loudener_gfx/handle.png https://raw.githubusercontent.com/samu4199/Loudener/main/loudener_gfx/handle.png
  loudener_gfx/button_off.png https://raw.githubusercontent.com/samu4199/Loudener/main/loudener_gfx/button_off.png
  loudener_gfx/button_on.png https://raw.githubusercontent.com/samu4199/Loudener/main/loudener_gfx/button_on.png
screenshot: GUI Example https://imgur.com/a/hM4leza
donation: Donate via PayPal https://www.paypal.com/paypalme/samuelepizzi
about:
  # Loudener
  ### A Sonnox Oxford Inflator-inspired distortion plugin that increases loudness without sacrificing dynamic range

  Author: Samuele Pizzi
  ---
  Many thanks to:
  - "tviler" from GearSpace
  - Edoardo "TheEdoRan" Ranghieri

//input_db
slider1:0<-6,12,0.1>-Input (dB)
//effect
slider2:100<0,100,0.1>-Effect (%)
//clip
slider3:1<0,1,1{OFF,ON}>-Clip 0 dB
//curve
slider4:0<-50,50,0.1>-Curve
//output_db
slider5:0<-12,0,0.1>-Output (dB)

//slider:multiband=0<0,1,1{OFF,ON}>Band Split

in_pin:Left Input
in_pin:Right Input
out_pin:Left Output
out_pin:Right Output

options:no_meter

filename:0,loudener_gfx/bg.png
filename:1,loudener_gfx/handle.png
filename:2,loudener_gfx/button_off.png
filename:3,loudener_gfx/button_on.png

@init
//db to gain conversion
last_gain = 10^(slider1 / 20);
olast_gain = 10^(slider5 / 20);

//waveshaping function, 0 dB clip
function waveShape(input) (
  a*abs(input)+b*(input^2)+c*abs(input^3)-d*(input^2-2*abs(input^3)+input^4);
);

//waveshaping function, +6 dB clip
function waveShapeOvershoot(input) (
  input = min(max(input, -2),2);
  2*abs(input) - input^2;
);

function process_sliders() (
  clip = slider3;
  //slider db to gain conversion on slider change
  next_gain = 10^(slider1 / 20);
  onext_gain = 10^(slider5 / 20);
  //waveshape coefficient computation on slider change
  a = 1 + (slider4 + 50)/100;
  b = -slider4/50;
  c = (slider4-50)/100;
  d = 1/16 - slider4/400 + (slider4^2)/(4*10^4);
  //wet-dry gain on effect slider change
  wet_gain = slider2/100;
  dry_gain = 1 - wet_gain;
);

@slider
//gain sliders value check
slider1 = min(max(slider1, -6),12);
slider5 = min(max(slider5, -12),0);
//assigning values to gui sliders
s1.value = slider1;
s2.value = slider2;
s4.value = slider4;
s5.value = slider5;
//process slider values in plugin parameters
process_sliders();

@block
//gain difference
d_gain = (next_gain - last_gain)/samplesblock;
od_gain = (onext_gain - olast_gain)/samplesblock;

@sample
//input gain
i_l = spl0 * last_gain;
i_r = spl1 * last_gain;
clip == 0 ? (
  //clip is off, apply waveShapeOvershoot to overshoots
  i_l > 1 || i_l < -1 ? (
    output_left = waveShapeOvershoot(i_l);
  ) : (
    output_left = waveShape(i_l);
  );
  i_r > 1 || i_r < -1 ? (
    output_right = waveShapeOvershoot(i_r);
  ) : (
    output_right = waveShape(i_r);
  );
) : (
  //clip is on, limit input to -1,1 then apply waveshaping function
  output_left = waveShape(min(max(i_l, -1),1));
  output_right = waveShape(min(max(i_r, -1),1));
);
//sample polarity + wet-dry gain + output gain
spl0 = (output_left * sign(spl0) * wet_gain + i_l * dry_gain) * olast_gain;
spl1 = (output_right * sign(spl1) * wet_gain + i_r * dry_gain) * olast_gain;
//gain difference
last_gain += d_gain;
olast_gain += od_gain;

@gfx 640 420
gfx_x = gfx_Y = 0;
gfx_blit(0,1,0);

function draw_slider(x,y,w,h,f,t,s,d,unit,title) (
  //change detecion
  this.value.temp != this.value ? (
    _sliderDirty = 1;
    this.value.temp = this.value;
  );

  //knob parameters
  this.knob.h = 44; //the actual handle height
  this.knob.hh = this.knob.h * 0.5;
  this.range = abs(f - t);

  //mouse logic
  mouse_x >= x && mouse_x <= x+w && mouse_y >= y && mouse_y <= y+h && !this.disabled ? (
    !mouse_cap ? this.hasEntered = 1;
    mouse_cap ? this.hasClicked = 1;
    this.hasEntered && this.hasClicked ? this.canChange = 1;
    ) : (
    this.hasEntered = this.hasClicked = 0;
  );
  !mouse_cap ? (this.canChange = 0; this.init_x = 0;);

  //process
  this.canChange ? (
    this.knob.input = -(mouse_y - y - h + this.knob.hh);
    this.init_y = 0;
    this.knob.input.normalized = (this.knob.input / (h-this.knob.hh+2));
    this.value.rect = this.range * this.knob.input.normalized;
    this.step = 1/s;
    this.value.rect = ceil(this.value.rect * this.step) / this.step;
    this.value = this.value.rect + f;

    mouse_cap & 4 ? this.value = d;
  );

  this.value = max(min(this.value,t),f);
  this.knob.pos = (this.range - this.value + f ) / (this.range) * (h-this.knob.h);

  //knob
  gfx_y = y+this.knob.pos-8; gfx_x = x;
  gfx_blit(1,1,0);

  //title
  gfx_r = gfx_g = gfx_b = 1;
  this.str1 = title;
  gfx_measurestr(this.str1,this.str1.w,this.str1.h);
  gfx_y = y - this.str1.h - 5; gfx_x = x + (w * 0.5) - (this.str1.w * 0.5);
  gfx_drawstr(this.str1);

  //readout
  !this.hidereadout ? (
    gfx_r = gfx_g = gfx_b = 1;
    this.str2 = strcat(sprintf(#,"%.1f",this.value),unit);
    gfx_measurestr(this.str2,this.str2.w,this.str2.h);
    gfx_y = y + h - 7; gfx_x = x + (w * 0.5) - (this.str2.w * 0.5);
    gfx_drawstr(this.str2);
  );

  this.value;
);

function draw_checkbox(x,y,w,h) (
  //change detecion
  this.checked.temp != this.checked ? (
    _sliderDirty = 1;
    this.checked.temp = this.checked;
  );

  //mouse logic
  mouse_x >= x && mouse_x <= x+w && mouse_y >= y && mouse_y <= y+h && !this.disabled ? (
    !mouse_cap ? this.hasEntered = 1;
    mouse_cap ? this.hasClicked = 1;
    this.hasEntered && this.hasClicked ? this.canChange = 1;
  ) : (
    this.hasEntered = this.hasClicked = this.canChange = 0;
  );
  !mouse_cap ? (this.canChange = 0;);

  this.canChange ? (
    this.checked = 1-this.checked;
    this.hasEntered = this.hasClicked = this.canChange = 0;
  );

  //frame
  //gfx_r = gfx_g = gfx_b = 0.35;
  //gfx_rect(x,y,w,h);
  //gfx_r = gfx_g = gfx_b = 0.07;
  //gfx_rect(x+1,y+1,w-2,h-2);

  //checked
  this.checked ? (
    gfx_x = x; gfx_y = y;
    gfx_blit(3,1,0);
  ) : (
    gfx_x = x; gfx_y = y;
    gfx_blit(2,1,0);
  );

  this.checked;
);

gfx_setfont(1,"Calibri",22);
sld_y = 78;
slider1 = s1.draw_slider(90,sld_y,45,290,-6,12,0.1,0," dB","");
slider2 = s2.draw_slider(194,sld_y,45,290,0,100,0.1,100," %","");
slider3 = s3.draw_checkbox(275,185,88,62);
slider4 = s4.draw_slider(402,sld_y,45,290,-50,50,0.1,0,"","");
slider5 = s5.draw_slider(506,sld_y,45,290,-12,0,0.1,0," dB","");

_sliderDirty ? (
  process_sliders();
  _sliderDirty = 0;
);
