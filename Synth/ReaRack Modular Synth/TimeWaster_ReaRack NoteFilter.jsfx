version: 1.1.0
desc:ReaRack - Note Filter
noindex: true
author: Malcom Smith
changelog: Improved handling of note off events. Added filter window colour change depending on pass state.

/* 1.0.0 (20/7/2017) Initial Release.
   1.1.0 (28/7/2017) Improved handling of note off events. Added filter window colour change depending on pass state.

Copyright(C) 2017 and later, Malcolm Smith (username 'Time Waster' on REAPER forums).
License: LGPL - http://www.gnu.org/licenses/lgpl.html
*/

slider1:chanout=1<0,16,1{All,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16}>MIDI Ouput Channel
slider2:edge1=0<0,127,1>Filter Window Edge 1
slider3:edge2=127<0,127,1>Filter Window Edge 2
slider5:strt=1<1,32,1>Note thinning Start note
slider6:nth=1<1,32,1>Note thinning Loop note
slider8:beatdiv1=0<0,24,1{1/64,1/32,1/16,1/8,1/4,1/3,1/2,2/3,1,1.5,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16}>Beat window Start beat
slider9:beatstrtft=0<-1,1,0.001>Start beat fine tune
slider10:beatdiv2=8<0,24,1{1/64,1/32,1/16,1/8,1/4,1/3,1/2,2/3,1,1.5,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16}>Beat window Loop beat
slider11:beatdiv3=8<0,24,1{1/64,1/32,1/16,1/8,1/4,1/3,1/2,2/3,1,1.5,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16}>Beat window Size
slider12:beatwinft=0<-1,1,0.001>Window size fine tune

in_pin:none
out_pin:none

@init
notecount = 0;
initstate = 1;

function beatSldr(beatdiv)
(
 beatdiv <= 4 ? 2^(beatdiv-6):
 beatdiv == 5 ? 1/3:
 beatdiv == 6 ? 0.5:
 beatdiv == 7 ? 2/3:
 beatdiv == 8 ? 1:
 beatdiv == 9 ? 1.5:
 beatdiv >=10 ? beatdiv-8;
 );

@block
initstate == 1 ? (
 initbeat = beat_position;
 initstate = 0;
 );
 
strt > nth ? (
  strt = nth;
  sliderchange(2^5); // The start note value can't be higer than the note loop length.
  );
beatdiv1 > beatdiv2 ? (
  beatdiv1 = beatdiv2;
  sliderchange(2^8); // The start beat value can't be higer than the beat loop length.
  );
beatdiv3 > beatdiv2 ? (
  beatdiv3 = beatdiv2;
  sliderchange(2^11);// The best window size value can't be higer than the beat loop length.
  );
  
beatstart = beatSldr(beatdiv1)+beatstrtft;
beatloop = beatSldr(beatdiv2);
beatsize = beatSldr(beatdiv3)+beatwinft;

beatpos = (beat_position-initbeat);
looppos = beatpos-(floor(beatpos/beatloop)*beatloop);

looppos <= beatsize ? beatwin = 1 : beatwin = 0;

/////////////////////////Get MIDI inputs/////////////////
// MIDI keyboard pitch control
while (midirecv(offset, msg1, msg2, msg3))
  (  
  // Extract message type and channel
  status = msg1 & $xF0;
  // Is it something other than a note on or note off event?
     status != $x90 && status != $x80 ?
      (
      //Pass through all
       midisend(offset, msg1, msg2, msg3);
       ):(
          // If it's a note on event, get note value
          status == $x90 ?
          (
          note = msg2 & $x7F;
          noteon = 1;
          notecount > nth-1 ? notecount = 0;
          notecount +=1;
          note[0] = 1;
          edge1 <= edge2 ?
            (
            note >= edge1 && note <= edge2 && notecount == strt && beatwin == 1 ?
              (
               midisend(offset, msg1, msg2, msg3);
               pass = 1;
               ):
               pass = 0;
            ):(
              note >= edge1 || note <= edge2 && notecount == strt && beatwin == 1?
                (
                 midisend(offset, msg1, msg2, msg3);
                 pass = 1;
                 ):
                 pass = 0;
              );
              
            ):
              // Otherwise, it's a note off event
              (
              note = msg2;
              noteon = 0;
              note[0] == 1 ?
                (
                 midisend(offset, msg1, msg2, msg3);
                 note[0] = 0;
                 );
            );
      );
);
      
@gfx 500 200
          
xscl = 1.7;
ox = 40;
oy = 40;
y2 = 15;

gfx_r = 1;
gfx_g = 0;
gfx_b = 0;
gfx_a = 1;

gfx_x = ox-12;
gfx_y = oy+5;
gfx_drawstr("0");
gfx_x = ox+xscl*127+5;
gfx_y = oy+5;
gfx_drawstr("127");

gfx_x = ox+xscl*edge1-3;
gfx_y = oy-13;
gfx_drawnumber(edge1,0);
gfx_x = ox+xscl*edge2-3;
gfx_y = oy+y2+5;
gfx_drawnumber(edge2,0);

gfx_r = 1;
gfx_g = 1;
gfx_b = 1;
gfx_a = 0.3;

gfx_x = ox;
gfx_y = oy;
gfx_lineto(ox+xscl*127,oy);
gfx_lineto(ox+xscl*127,oy+y2);
gfx_lineto(ox,oy+y2);
gfx_lineto(ox,oy);

gfx_r = 1;
gfx_g = 1;
gfx_b = 1;
gfx_a = 1;

edge1 <= edge2 ?
  (
  pass == 1 ?
  (
  gfx_r = 0;
  gfx_g = 1;
  gfx_b = 0;
  ):
  (
  gfx_r = 1;
  gfx_g = 0;
  gfx_b = 0;
  );
  gfx_x = ox+xscl*edge1;
  gfx_y = oy;
  gfx_lineto(ox+(xscl*edge2),oy);
  gfx_lineto(ox+(xscl*edge2),oy+y2);
  gfx_lineto(ox+(xscl*edge1),oy+y2);
  gfx_lineto(ox+(xscl*edge1),oy);
  
  noteon == 1 ?
  (
  note >= edge1 && note <= edge2 && notecount == strt && beatwin == 1 ?
    (
    gfx_r = 0;
    gfx_g = 1;
    gfx_b = 0;
    gfx_a = 1;
    ):(
      gfx_r = 1;
      gfx_g = 0;
      gfx_b = 0;
      gfx_a = 0.5;
      );
    gfx_x = ox+(note*xscl);
    gfx_y = oy+y2;
    gfx_lineto(ox+(xscl*note),oy);
    );
  ):(
    pass == 1 ?
    (
    gfx_r = 0;
    gfx_g = 1;
    gfx_b = 0;
   ):
    (
    gfx_r = 1;
    gfx_g = 0;
    gfx_b = 0;
    );
    gfx_x = ox;
    gfx_y = oy;
    gfx_lineto(ox+(xscl*edge2),oy);
    gfx_lineto(ox+(xscl*edge2),oy+y2);
    gfx_lineto(ox,oy+y2);
    gfx_lineto(ox,oy);
    gfx_x = ox+xscl*edge1;
    gfx_y = oy;
    gfx_lineto(ox+(xscl*127),oy);
    gfx_lineto(ox+(xscl*127),oy+y2);
    gfx_lineto(ox+(xscl*edge1),oy+y2);
    gfx_lineto(ox+(xscl*edge1),oy);
    noteon == 1 ?
    (
    note <= edge2 || note >= edge1 && notecount == strt && beatwin == 1 ?
      (
      gfx_r = 0;
      gfx_g = 1;
      gfx_b = 0;
      gfx_a = 1;
      ):(
        gfx_r = 1;
        gfx_g = 0;
        gfx_b = 0;
        gfx_a = 0.5;
        );
      gfx_x = ox+(note*xscl);
      gfx_y = oy+y2;
      gfx_lineto(ox+(xscl*note),oy);
      );
);

