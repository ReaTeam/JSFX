desc:sin/cos oscillator lib rev 00
/*
 Copyright 2014, Thierry Rochebois
 All rights reserved.

  Redistribution and use in source and binary forms, with or without 
  modification, are permitted provided that the following conditions are
  met:
  Redistributions of source code must retain the above copyright notice,
  this list of conditions and the following disclaimer. 

  Redistributions in binary form must reproduce the above copyright
  notice, this list of conditions and the following disclaimer in the
  documentation and/or other materials provided with the distribution. 

  The name of Thierry Rochebois may not be used to endorse or promote
  products derived from this software without specific prior written
  permission. 

  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS 
  "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
  LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
  A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT 
  OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
  SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT 
  LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
  DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
  THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT 
  (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
  OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/
/*
ratios
sliderN:0<0,19,1{2,1/2,3/2,2 + 1/2,1/4,1 - 1/4,1 + 1/4,2 - 1/4,2 + 1/4,1/2 - 1/8,1/2 + 1/8,1 - 1/8,1 + 1/8,2 - 1/8,2 + 1/8,3,3 - 1/2,3 + 1/2,3 - 1/4,3 + 1/4}>Ratio

*/
@init
//______________________________________________________________________
function SINI()
local(p p16 w dw) (
  ad === 0 ? ad = 1;
  sr === 0 ? sr = srate;
  
// _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _
  SIN_tRatio = ad; ad += 20;
  SIN_tRatio[0] = 2;
  SIN_tRatio[1] = 1/2;
  SIN_tRatio[2] = 3/2;
  SIN_tRatio[3] = 5/2;
  SIN_tRatio[4] = 1/4;
  SIN_tRatio[5] = 1-1/4;
  SIN_tRatio[6] = 1+1/4;
  SIN_tRatio[7] = 2 - 1/4;
  SIN_tRatio[8] = 2+1/4;
  SIN_tRatio[9] = 1/2-1/8;
  SIN_tRatio[10] = 1/2+1/8;
  SIN_tRatio[11] = 1-1/8;
  SIN_tRatio[12] = 1+1/8;
  SIN_tRatio[13] = 2-1/8; 
  SIN_tRatio[14] = 2+1/8;
  SIN_tRatio[15] = 3;
  SIN_tRatio[16] = 3-1/2;
  SIN_tRatio[17] = 3+1/2;
  SIN_tRatio[18] = 3-1/4;
  SIN_tRatio[19] = 3+1/4;
  
// _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _
  SIN_tDP = ad; ad += 128 * 16 + 1;
  SIN_tDPD = ad; ad += 128 * 16 + 1;
  p = 0;
  p16 = 0;
  loop(128*16+1,
    w = (512*440/sr) * 2 ^((p-69)*(1/12));
    SIN_tDP[p16] = w;
    w = (512*440/sr) * 2 ^((p+(1/16)-69)*(1/12));
    SIN_tDPD[p16] = w-SIN_tDP[p16];
    p16 += 1;
    p += 1/16;    
  );
  SIN_coefFToDp = 512 / sr;
// _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _
  SIN_tSin513 = ad; ad += 513;
  SIN_tCos513 = ad; ad += 513;
  SIN_tSinD513 = ad; ad += 513;
  SIN_tCosD513 = ad; ad += 513;
  dw =  2*$pi/512;
  w = 0;
  p = 0;
  loop(513,
    SIN_tSin513[p] = cos(w - 0.5 * $pi);
    SIN_tCos513[p] = cos(w);
    SIN_tSinD513[p] = cos(w + dw - 0.5 * $pi) - SIN_tSin513[p];
    SIN_tCosD513[p] = cos(w + dw) - SIN_tCos513[p];
    p += 1;
    w += dw;
  );
);

//______________________________________________________________________
function SIN_pitchToDp(p16) 
local (p0)(
  p16 = min(128*16, max(0, p16));
  p0 = p16|0;
  SIN_tDP[p0] + (p16 - p0) * SIN_tDPD[p0];
  //(512*440/srate) * 2 ^((p16/16-69)*(1/12));
);

//______________________________________________________________________
function SIN_init()(
  this.p = 0;
  this.dp = 0.1;
);
//______________________________________________________________________
//p16 : base pitch, without bend
//bend: [-1,1]
//h : static harmonic (0 1 2 3/2 4/3 1/2 1/3)
function SIN_freqShifterSet(p16 dpc h bend)(
  this.dp = SIN_pitchToDp(p16) * (dpc + h * (1 - 2^bend));
);
// _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _
function SIN_freqShifterProc(y* x*)
local (p0 a)(
  this.p += this.dp;
  this.p <     0 ? this.p += 512;
  this.p >=  512 ? this.p -= 512;
//_  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _
  p0 = this.p|0;
  a = this.p - p0;
  this.i = SIN_tSin513[p0] + a * SIN_tSinD513[p0];
  this.r = SIN_tCos513[p0] + a * SIN_tCosD513[p0];
//_  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _
  y.r = x.r * this.r - x.i * this.i;
  y.i = x.i * this.r + x.r * this.i;
);
//______________________________________________________________________
function SIN_kProc(p16 nRatio offset)(
  this.dp = SIN_pitchToDp(p16) * SIN_tRatio[nRatio] 
          + SIN_coefFToDp * offset;
);
//______________________________________________________________________
//            out in this.r & this.i  
function SIN_aProc() 
local (p0 a)(
  this.p += this.dp;
  this.p >=  512 ? this.p -= 512;
  p0 = this.p|0;
  a = this.p - p0;

  this.i = SIN_tSin513[p0] + a * SIN_tSinD513[p0];
  this.r = SIN_tCos513[p0] + a * SIN_tCosD513[p0];
);
