desc:ADSR library rev03
/*
 Copyright 2014, Thierry Rochebois
 All rights reserved.

  Redistribution and use in source and binary forms, with or without 
  modification, are permitted provided that the following conditions are
  met:
  Redistributions of source code must retain the above copyright notice,
  this list of conditions and the following disclaimer. 

  Redistributions in binary form must reproduce the above copyright
  notice, this list of conditions and the following disclaimer in the
  documentation and/or other materials provided with the distribution. 

  The name of Thierry Rochebois may not be used to endorse or promote
  products derived from this software without specific prior written
  permission. 

  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS 
  "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
  LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
  A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT 
  OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
  SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT 
  LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
  DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
  THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT 
  (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
  OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/

//_____________________________________________________________________
@init

function ADSR_p(n A D S R) (
  ADSR_tA[n] = A;
  ADSR_tD[n] = D;
  ADSR_tS[n] = S;
  ADSR_tR[n] = R;
);
//_____________________________________________________________________
//                    Static resources & tables for all ADSR "objects"
function ADSR()
  local() (
  ADSR_time_coef = 2.71 / (srate/KRATE);
  
  ADSR_nbPresets = 17;
  
  ad === 0 ? ad = 1;
  ADSR_tA = ad; ad += ADSR_nbPresets;
  ADSR_tD = ad; ad += ADSR_nbPresets;
  ADSR_tS = ad; ad += ADSR_nbPresets;
  ADSR_tR = ad; ad += ADSR_nbPresets;
  //<0,16,1{Piano,Organ,Pad,Slow,Very Slow,Reverso,Breathaway,Harp,Gate,Bass,Fast Bass,Long,Very Long,Fast Reverso,Fast Breathaway,Smooth Bass,Oboe}>
  ADSR_p( 0, 0.003, 5.0, 0.0, 0.400);  //Piano
  ADSR_p( 1, 0.2,   5.0, 1.0, 0.501);  //Organ
  ADSR_p( 2, 0.7  , 5.0, 0.8, 2.000);  //Pad
  ADSR_p( 3, 3    , 5.0, 0.8, 3.000);  //Slow
  ADSR_p( 4, 6    , 5.0, 0.8, 5.000);  //Very Slow
  ADSR_p( 5, 5    , 0.001, 0, 0.001);  //Reverso
  ADSR_p( 6, 4    , 6.0, 0.3, 5.000);  //Breathaway
  ADSR_p( 7, 0.003, 1.0, 0.0,10.000);  //Harp
  ADSR_p( 8, 0.001, 0.5, 1.0, 0.001);  //Gate
  ADSR_p( 9, 0.01,  0.4, 0.0, 0.6);    //Bass
  ADSR_p(10, 0.001, 0.25,0.0, 0.2);    //Fast Bass
  ADSR_p(11, 0.01,  10,  0.0, 0.3);    //Long
  ADSR_p(12, 0.01,  20,  0.0, 0.3);    //Very Long
  ADSR_p(13, 1    , 0.001, 0, 0.001);  //Fast Reverso
  ADSR_p(14, 1    , 1.0, 0.3, 1.000);  //Fast Breathaway
  ADSR_p(15, 0.2  , 0.5,   0, 0.3);    //Smooth Bass
  ADSR_p(16, 0.3  , 5.0, 0.5, 0.1);    //Oboe
);
function ADSR_setTimeFactor(timeFactor)(
  timeFactor = max(0.25, timeFactor);
  this.timeFactor1 = 1 / timeFactor;
);
//_____________________________________________________________________
//                                              Init of a ADSR instance
function ADSR_set(A D S R) (
  A > 0 ?
    this.A = ADSR_time_coef / A
  : this.A = 1;
  this.A = min(this.A, 0.25);
  
  D > 0 ?
    this.D = ADSR_time_coef / D
  : this.D = 1;
  this.D = min(this.D, 0.25);
  
  this.S = S;
  
  R > 0 ?
    this.R = ADSR_time_coef / R
  : this.R = 1;
  this.R = min(this.R, 0.25);
);


function ADSR_init(A D S R) (
  this.ADSR = 0;
  this.ASR = 0;
  this.AttackSeg = 0;
  this.timeFactor1 = 1;
  this.ADSR_set(A, D, S, R);
 
);
//_____________________________________________________________________
//                                                              presets
function ADSR_preset(n) (
  n=min(ADSR_nbPresets,max(0,n));
  this.ADSR_set(ADSR_tA[n], ADSR_tD[n], ADSR_tS[n], ADSR_tR[n]);
);

//_____________________________________________________________________
//                                              Control rate processing
function ADSR_kProc(gate trig)
  local() (
  trig ? this.AttackSeg = 1;
  this.ASR < gate ?
    this.ASR += this.timeFactor1 * this.A * (gate - this.ASR)
  : this.ASR += this.timeFactor1 * this.R * (gate + 0.00000001 - this.ASR);
//  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _  _
  this.AttackSeg === 1 ? (
    this.ADSR += this.timeFactor1 * this.A * (1.5 - this.ADSR);
    this.ADSR >= 1.0 ? 
    ( this.ADSR = 1.0;
      this.AttackSeg = 0;
    );
  );
  this.AttackSeg == 0 ? (
    gate != 0 ? (
      this.ADSR += this.timeFactor1 * this.D * (this.S - this.ADSR);
    ) : ( 
      this.ADSR += this.timeFactor1 * this.R * (0.00000001 - this.ADSR);
    );
  );
  this.ASR * this.ADSR;
);
