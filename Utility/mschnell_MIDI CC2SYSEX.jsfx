desc: MIDI CC to SysEx
author: Michael Schnell (mschnell@bschnell.de)
version: 1.0
changelog: initial release
about:
  ## Description
    
    Up to three CC messages are converted to SysEx messages.
  
    The slider "CC SysEx" selects the file that is to contain the the definition of the SysEx messages. The file needs to be located in the Reaper folder "Data/cc sysex" and be named with a .txt extension. (File format see below)
  
    The slider "MIDI Input Channel" selects the channel on which all converted CCs are received.
  
    The sliders "CC Input 1", "CC Input 2", and "CC Input 3" define the up to three messages to be converted. 
  
    Slider "All Messages": "Pass Through" -> all but the converted CC messages are passed through. "Block" -> only the SysEx Messages generated are sent. 
  
    A Graphic shows the op to three complete resulting SysEyx messages including header and trailer and the last inserted CC value.
  
    ## File format
  
    The configuration file can feature one, two or three lines. Accordingly there are one, two or three different CC messages to be converted to SysEx. 
  
    Each line consists of a series of up to 100 byte definitions, each of which is given by two hexadecimal digits and are separated by a space. A single of these definitions can optionally be be "XX" as a placeholder for the CC value to be transferred. The leading "F0" and trailing "F7" bytes necessary to form a SysEx message are not given, as the software inserts these automatically. (The bytes transferred in a SysEx message need to be lower or equal to 7f). 
    Example : 00 21 10 78 3d 17 XX 7F
 
    ## Limitations
  
    The current version directly inserts the 7 bit CC value in the SysEx message. So higher resolution values are not supported. 
  
    Sometimes switching between different files does not work correctly. Pleas ctr-Click the enable box in the track's FX chain twice to see the correct new file.
donation: United Nations Foundation http://www.unfoundation.org/

slider1:/cc sysex:none:CC SysEx
slider2:0<0,15,1{1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16}>MIDI Input Channel
slider3:1<0,127,1{0 Bank Sel M,1 Mod Wheel M,2 Breath M,3,4 Foot P M,5 Porta M,6 Data Entry M,7 Vol M,8 Balance M,9,10 Pan M,11 Expression M,12 Ctrl 1 M,13 Ctrl 2 M,14,15,16 GP Slider 1,17 GP Slider 2,18 GP Slider 3,19 GP Slider 4,20,21,22,23,24,25,26,27,28,29,30,31,32 Bank Sel L,33 Mod Wheel L,34 Breath L,35,36 Foot P L,37 Porta L,38 Data Entry L,39 Vol L,40 Balance L,41,42 Pan L,43 Expression L,44 Ctrl 1 L,45 Ctrl 2 L,46,47,48,49,50,51,52,53,54,55,56,57,58,59,60,61,62,63,64 Hold P sw,65 Porta sw,66 Sustenuto sw,67 Soft P sw,68 Legato P sw,69 Hold 2 P sw,70 S.Variation,71 S.Timbre,72 S.Release,73 S.Attack,74 S.Brightness,75 S.Ctrl 6,76 S.Ctrl 7,77 S.Ctrl 8,78 S.Ctrl 9,79 S.Ctrl 10,80 GP B.1 sw,81 GP B.2 sw,82 GP B.3 sw,83 GP B.4 sw,84,85,86,87,88,89,90,91 Effects Lv,92 Trem Lv,93 Chorus Lv,94 Celeste Lv,95 Phaser Lv,96 Data B. Inc,97 Data B. Dec,98 NRP L,99 NRP M,100 RP L,101 RP M,102,103,104,105,106,107,108,109,110,111,112,113,114,115,116,117,118,119,120,121,122,123,124,125,126,127}>CC Input 1
slider4:1<0,127,1{0 Bank Sel M,1 Mod Wheel M,2 Breath M,3,4 Foot P M,5 Porta M,6 Data Entry M,7 Vol M,8 Balance M,9,10 Pan M,11 Expression M,12 Ctrl 1 M,13 Ctrl 2 M,14,15,16 GP Slider 1,17 GP Slider 2,18 GP Slider 3,19 GP Slider 4,20,21,22,23,24,25,26,27,28,29,30,31,32 Bank Sel L,33 Mod Wheel L,34 Breath L,35,36 Foot P L,37 Porta L,38 Data Entry L,39 Vol L,40 Balance L,41,42 Pan L,43 Expression L,44 Ctrl 1 L,45 Ctrl 2 L,46,47,48,49,50,51,52,53,54,55,56,57,58,59,60,61,62,63,64 Hold P sw,65 Porta sw,66 Sustenuto sw,67 Soft P sw,68 Legato P sw,69 Hold 2 P sw,70 S.Variation,71 S.Timbre,72 S.Release,73 S.Attack,74 S.Brightness,75 S.Ctrl 6,76 S.Ctrl 7,77 S.Ctrl 8,78 S.Ctrl 9,79 S.Ctrl 10,80 GP B.1 sw,81 GP B.2 sw,82 GP B.3 sw,83 GP B.4 sw,84,85,86,87,88,89,90,91 Effects Lv,92 Trem Lv,93 Chorus Lv,94 Celeste Lv,95 Phaser Lv,96 Data B. Inc,97 Data B. Dec,98 NRP L,99 NRP M,100 RP L,101 RP M,102,103,104,105,106,107,108,109,110,111,112,113,114,115,116,117,118,119,120,121,122,123,124,125,126,127}>CC Input 2
slider5:1<0,127,1{0 Bank Sel M,1 Mod Wheel M,2 Breath M,3,4 Foot P M,5 Porta M,6 Data Entry M,7 Vol M,8 Balance M,9,10 Pan M,11 Expression M,12 Ctrl 1 M,13 Ctrl 2 M,14,15,16 GP Slider 1,17 GP Slider 2,18 GP Slider 3,19 GP Slider 4,20,21,22,23,24,25,26,27,28,29,30,31,32 Bank Sel L,33 Mod Wheel L,34 Breath L,35,36 Foot P L,37 Porta L,38 Data Entry L,39 Vol L,40 Balance L,41,42 Pan L,43 Expression L,44 Ctrl 1 L,45 Ctrl 2 L,46,47,48,49,50,51,52,53,54,55,56,57,58,59,60,61,62,63,64 Hold P sw,65 Porta sw,66 Sustenuto sw,67 Soft P sw,68 Legato P sw,69 Hold 2 P sw,70 S.Variation,71 S.Timbre,72 S.Release,73 S.Attack,74 S.Brightness,75 S.Ctrl 6,76 S.Ctrl 7,77 S.Ctrl 8,78 S.Ctrl 9,79 S.Ctrl 10,80 GP B.1 sw,81 GP B.2 sw,82 GP B.3 sw,83 GP B.4 sw,84,85,86,87,88,89,90,91 Effects Lv,92 Trem Lv,93 Chorus Lv,94 Celeste Lv,95 Phaser Lv,96 Data B. Inc,97 Data B. Dec,98 NRP L,99 NRP M,100 RP L,101 RP M,102,103,104,105,106,107,108,109,110,111,112,113,114,115,116,117,118,119,120,121,122,123,124,125,126,127}>CC Input 3
slider6:0<0,1,1{Pass Through,Block}>All Messages
@init

//#font = "Courier New";
//#font = "Liberation Mono";
#font = "";  // Default
sysex = 0; 
sb = sysex + 1;
si = 1000;
ci = 1010;
cc = 1020;



function slider_file_to_numbered_strings(slidernumber,offset)local(handle,lastvalue,lines) (
//USE ONLY THE NUMBER OF THE SLIDER FOR "slidernumber"  - do not use "sliderx"
//  slider(slidernumber) != lastvalue ? reload=0;
  
  handle=file_open(slider(slidernumber));
  file_string(handle,offset);  
  while( strlen(offset) && offset < 1023 )
    ( offset+=1;
      file_string(handle,offset);      
    );
  file_close(file);
  lines=offset;
  lastvalue=slider(slidernumber);
  reload=1;
  lines;
); 
  
 
 
@slider
  inChannel   = slider2;
  cc[0]       = slider3;
  cc[1]       = slider4;
  cc[2]       = slider5;
  memset (sysex, 0xFF, 3*100);
  sysex[0*100] = 0xF0;
  sysex[1*100] = 0xF0;
  sysex[2*100] = 0xF0;
  lines = slider_file_to_numbered_strings(1, 0);
//  #s = offset;
  
  
//  ___x = strlen(#s);
  
  row = 0;
  loop (lines, 
    end = 0;
    o = 0;
    si[row] = 100*row;  
    #s = strcpy(#, row);
    while (!end && (o < strlen(#s)-1)) (
      c = str_getchar(#s, o, 'c');
      c >= 0x60 ? c -= 0x20;
      (c >= '0') && (c <= '9') ? (
        j = c - 0x30;
       ) : (c >= 'A') && (c <= 'F') ? (
        j = c - 0x37;
       ) : c == 'X' ? (
        end = 2; 
       ) : (
        end = 1; 
      );
      i = j;   
      o +=1;
      c = str_getchar(#s, o, 'c');
      c >= 0x60 ? c -= 0x20;
      (c >= '0') && (c <= '9') ? (
        j = c - 0x30;
       ) : (c >= 'A') && (c <= 'F') ? (
        j = c - 0x37;
       ) : (c == 'X') && (end == 2) ? (
        ci[row] = si[row];
        end = 3;
       ) : (
        end = 1; 
      );
      i = (i << 4) + j;
      o += 1;
      end == 3 ? (
        end = 0;
        sb[si[row]] = 0x80;
       ) : ( 
        sb[si[row]] = i;
      );  
      si[row] += 1;    
      o += 1;    
    ); 
    sysex[si[row]+1] = 0xF7;
    row += 1; 
  );
  //____si0 = si[0];
  //____si1 = si[1];
  //____si2 = si[2];
  //____ci0 = ci[0];
  //____ci1 = ci[1];
  //____ci2 = ci[2];


@block
  while (midirecv(offset, msg1, msg2, msg3)) (
    status = msg1 & $xF0;      // Extract message type
    channel = msg1 & $x0F;
    channel == inChannel ? (   // Is it on our channel?
      status == $xB0 ? (       // Is it a controller event?
        row = 0;
        loop (lines, 
          msg2 == cc[row] ? (      // Is it the right CC?
            msg1 = 0;
            sb[ci[row]] = msg3;
//            ____  = msg3;
            midisend_buf(offset,sysex+row*100,(si[row] % 100)+2)
          );  
          row += 1;      
        );
      );
    );    
    msg1 && !Slider6 ? midisend(offset, msg1, msg2, msg3); // pass through
  );
  
@gfx 650 450  
  //display file
  gfx_r=gfx_g=gfx_b=0; gfx_a=1;
  gfx_x=gfx_y=0;
  gfx_rectto(gfx_w,gfx_h);
  
  gfx_setfont(2,#font,18);
//  textw = gfx_texth * 0.6;
  lines ? (
    row = 0;
    loop(lines,
//      gfx_x=10;
//      gfx_y=10 + 40 * row;
//      #s = strcpy(#, row);
//      gfx_r=gfx_g=gfx_b=1;
//      gfx_drawstr(#s);
      gfx_x=10;
      gfx_y=10 + 20 * row;
      _i = 100*row;
      loop ((si[row] % 100) +2,
        err = 0;
        sysex[_i] & 0x80 ? (
          (sysex[_i] == 0xF0) ||  (sysex[_i] == 0xF7) ? (
            #_s = sprintf(#, "%2.2X ", sysex[_i]);   
           ) : ( 
            #_s = "?? ";
            err = 1;
          );  
         ) : ( 
          #_s = sprintf(#, "%2.2X ", sysex[_i]);   
        );  
        _i != ci[row]+1 ? (
          !err ? (
            gfx_r=0;gfx_g=1;gfx_b=1;
          ) : (  
            gfx_r=1;gfx_g=0;gfx_b=0;
          );
         )  : ( 
          gfx_r=1;gfx_g=1;gfx_b=0;            
        );  
        gfx_drawstr(#_s);
        _i += 1;
      );  
      row += 1;
    );  
   ) : ( 
    gfx_x=10;
    gfx_y=10;
    gfx_r=1;gfx_g=0;gfx_b=0;
    gfx_drawstr("No appropriate *.txt file in   >>>>  .../REAPER/Data/CC SysEx  <<<<");    
  );  
